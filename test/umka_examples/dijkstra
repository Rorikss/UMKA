class Edge {
    let v = unit;
    let u = unit;
    let w = unit;
}

fun mk_edge(v, u, w) -> Edge {
    let e = Edge;
    e:v = v;
    e:u = u;
    e:w = w;
    return e;
}

fun mk_array(n, x) -> [] {
    let arr = [];
    for (let i = 0; i < n; i = i + 1) {
        add(arr, x);
    }
    return arr;
}

fun Dijkstra(graph, edges, s) -> [] {
    let n = len(graph);
    let inf = 1000000000;
    let dist = mk_array(n, inf);
    let q = [];
    set(dist, s, 0);
    push_heap(q, [0, s]);
    while (len(q) > 0) {
        let d_v = get(q, 0);
        let v = get(d_v, 1);
        pop_heap(q);
        if (get(dist, v) == -get(d_v, 0)) {
            for (let i = 0; i < len(get(graph, v)); i = i + 1) {
                let edge = get(get(graph, v), i);
                if (get(dist, v) + edge:w < get(dist, edge:u)) {
                    set(dist, edge:u, get(dist, v) + edge:w);
                    push_heap(q, [-get(dist, edge:u), edge:u]);
                }
            }
        }
    }
    for (let i = 0; i < n; i = i + 1) {
        if (get(dist, i) == inf) {
            set(dist, i, -1);
        }
    }
    return dist;
}

//     (A)---2---(B)---3---(C)
//     / \       / \       / \
//    1   3     4   2     1   4
//   /     \   /     \   /     \
// (D)--2--(E)---5---(F)---3---(G)
//   \     / \       / \       /
//    4   3   1     2   2     3
//     \ /     \   /     \   /
//     (H)---6--(I)---4---(J)

fun main() -> unit {
    let n = 10;
    let g = [];
    for (let i = 0; i < n; i = i + 1) {
        add(g, []);
    }

    let edges = [
        [1, 2, 2],
        [1, 4, 1],
        [1, 5, 3],
        [2, 3, 3],
        [2, 5, 4],
        [2, 6, 2],
        [3, 6, 1],
        [3, 7, 4],
        [4, 5, 2],
        [4, 8, 4],
        [5, 6, 5],
        [5, 8, 3],
        [5, 9, 1],
        [6, 7, 3],
        [6, 9, 2],
        [6, 10, 2],
        [7, 10, 3],
        [8, 9, 6],
        [9, 10, 4]
    ];

    for (let i = 0; i < len(edges); i = i + 1) {
        let edge = get(edges, i);
        let e = mk_edge(get(edge, 0) - 1, get(edge, 1) - 1, get(edge, 2));
        add(get(g, e:v), mk_edge(e:v, e:u, e:w));
        add(get(g, e:u), mk_edge(e:u, e:v, e:w));
    }

    let dist = Dijkstra(g, edges, 0);
    print(dist);

}